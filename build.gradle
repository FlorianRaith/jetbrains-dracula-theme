import org.jetbrains.intellij.tasks.RunPluginVerifierTask

plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.3.72"
    id "org.jetbrains.intellij" version "0.6.5"
}

group = "com.draculatheme"
version = "1.10.6"

compileJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.72"
}

intellij {
    version = "2019.2"
    type = "IC"
}

buildSearchableOptions {
    enabled = false
}

patchPluginXml {
    sinceBuild = "192.0"
    untilBuild = "211.*"

    def changelogFile = file("${project.buildDir}/CHANGELOG.html")
    def readmeFile = file("${project.buildDir}/README.html")

    if (changelogFile.exists()) {
        changeNotes changelogFile.text
    }
    if (readmeFile.exists()) {
        pluginDescription readmeFile.text
    }
}

runPluginVerifier {
    ideVersions = ["192.7142.36", "193.7288.26", "201.8743.12", "202.8194.7", "203.7148.57"]
    failureLevel = EnumSet.of(RunPluginVerifierTask.FailureLevel.COMPATIBILITY_PROBLEMS, RunPluginVerifierTask.FailureLevel.INVALID_PLUGIN)
}

publishPlugin {
    token System.getProperty("jetbrains.token")
}